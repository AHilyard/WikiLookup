buildscript {
	repositories {
		maven { url = 'https://maven.minecraftforge.net' }
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
	}
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

base.archivesName = project.name + '-' + project.mcVersion

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
	mappings channel: 'official', version: project.mcVersion

	runs {
		client {
			properties 'forge.logging.markers': ''
			properties 'forge.logging.console.level': 'debug'
			properties 'terminal.ansi': 'true'
			workingDirectory project.file('run').canonicalPath
			source sourceSets.main

			mods {
				wikilookup {
					source sourceSets.main
				}
			}
		}
	}
}

sourceSets {
	main {
		resources {
			srcDir 'src/generated/resources'
		}
	}
}

repositories {
	maven {
		name = "CurseMaven"
		url "https://www.cursemaven.com"
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${project.mcVersion}-${project.forgeVersion}"
	annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
	implementation fg.deobf("curse.maven:iceberg-520110:4744147")
}

jar {
	manifest {
		attributes(["Specification-Title":		project.name,
					"Specification-Vendor":		project.author,
					"Specification-Version":	project.version,
					"Implementation-Title":		project.name,
					"Implementation-Version":	project.version,
					"Implementation-Vendor" :	project.author,
					"Implementation-Timestamp":	new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")])
	}
}
